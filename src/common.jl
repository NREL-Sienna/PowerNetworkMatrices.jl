function _add_to_collection!(
    collection_br::Vector{PSY.ACTransmission},
    branch::PSY.ACTransmission,
)
    push!(collection_br, branch)
    return
end

"""
    _build_bus_to_valid_idx(n_buses, valid_ix) -> Vector{Int}

Build the inverse of `valid_ix`: a length-`n_buses` vector where entry `b`
is the position of bus `b` inside `valid_ix`, or 0 if `b` is a reference
bus. Used by the Virtual\\* row-computation hot path so it can iterate the
nonzeros of a sparse `BA` column directly (O(nnz_col)) instead of scanning
the full bus axis (O(n_buses)) and bisecting the CSC for each entry.
"""
function _build_bus_to_valid_idx(n_buses::Int, valid_ix::Vector{Int})
    bus_to_valid_idx = zeros(Int, n_buses)
    @inbounds for (i, b) in enumerate(valid_ix)
        bus_to_valid_idx[b] = i
    end
    return bus_to_valid_idx
end

function _add_to_collection!(
    collection_tr3w::Vector{PSY.ThreeWindingTransformer},
    transformer_tr3w::PSY.ThreeWindingTransformer,
)
    push!(collection_tr3w, transformer_tr3w)
    return
end

function get_bus_index(bus_no::Int, bus_lookup::Dict{Int, Int}, nr::NetworkReductionData)
    if haskey(nr.reverse_bus_search_map, bus_no)
        return bus_lookup[nr.reverse_bus_search_map[bus_no]]
    else
        return bus_lookup[bus_no]
    end
end

function get_bus_index(
    dev::PSY.Component,
    bus_lookup::Dict{Int, Int},
    nr::NetworkReductionData,
)
    bus_number = PSY.get_number(PSY.get_bus(dev))
    return get_bus_index(bus_number, bus_lookup, nr)
end

function get_bus_indices(
    arc::PSY.Arc,
    bus_lookup::Dict{Int, Int},
    nr::NetworkReductionData,
)
    check_arc_validity(arc, IS.get_name(arc))
    reverse_bus_search_map = get_reverse_bus_search_map(nr)
    fr_bus_number = PSY.get_number(PSY.get_from(arc))
    if haskey(reverse_bus_search_map, fr_bus_number)
        fr_bus_number_reduced = reverse_bus_search_map[fr_bus_number]
    else
        fr_bus_number_reduced = fr_bus_number
    end
    fr_bus_ix = bus_lookup[fr_bus_number_reduced]

    to_bus_number = PSY.get_number(PSY.get_to(arc))
    if haskey(reverse_bus_search_map, to_bus_number)
        to_bus_number_reduced = reverse_bus_search_map[to_bus_number]
    else
        to_bus_number_reduced = to_bus_number
    end
    to_bus_ix = bus_lookup[to_bus_number_reduced]
    return fr_bus_ix, to_bus_ix
end

function check_arc_validity(arc::PSY.Arc, name::String)
    if PSY.get_bustype(PSY.get_from(arc)) == ACBusTypes.ISOLATED
        throw(
            IS.ConflictingInputsError(
                "Branch or arc $(name) is set available and connected to isolated bus " *
                "$(IS.get_name(PSY.get_from(arc)))",
            ),
        )
    end
    if PSY.get_bustype(PSY.get_to(arc)) == ACBusTypes.ISOLATED
        throw(
            IS.ConflictingInputsError(
                "Branch or arc $(name) is set available and connected to isolated bus " *
                "$(IS.get_name(PSY.get_to(arc)))",
            ),
        )
    end
    return
end

function get_arc_tuple(arc::PSY.Arc, nr::NetworkReductionData)
    reverse_bus_search_map = get_reverse_bus_search_map(nr)
    arc_tuple_original = get_arc_tuple(arc)
    return (
        get(reverse_bus_search_map, arc_tuple_original[1], arc_tuple_original[1]),
        get(reverse_bus_search_map, arc_tuple_original[2], arc_tuple_original[2]),
    )
end

function get_arc_tuple(
    tr::ThreeWindingTransformerWinding,
    nr::NetworkReductionData,
)
    reverse_bus_search_map = get_reverse_bus_search_map(nr)
    arc_tuple_original = get_arc_tuple(tr)
    return (
        get(reverse_bus_search_map, arc_tuple_original[1], arc_tuple_original[1]),
        get(reverse_bus_search_map, arc_tuple_original[2], arc_tuple_original[2]),
    )
end

function get_arc_tuple(br::PSY.ACTransmission, nr::NetworkReductionData)
    get_arc_tuple(PSY.get_arc(br), nr)
end

# Parallel branches: all oriented in same direction, so just take arc of first.
function get_arc_tuple(br::AbstractBranchesParallel, nr::NetworkReductionData)
    get_arc_tuple(PSY.get_arc(first(br)), nr)
end

function get_arc_tuple(br::AbstractBranchesParallel)
    return get_arc_tuple(PSY.get_arc(first(br)))
end

function get_arc_tuple(br::PSY.ACTransmission)
    return get_arc_tuple(PSY.get_arc(br))
end

get_arc_tuple(arc::PSY.Arc) =
    (PSY.get_number(PSY.get_from(arc)), PSY.get_number(PSY.get_to(arc)))

function get_switched_admittances(sys::PSY.System, reverse_bus_search_map)
    collection = Vector{PSY.SwitchedAdmittance}()
    for sa in
        collect(PSY.get_components(x -> PSY.get_available(x), PSY.SwitchedAdmittance, sys))
        if !haskey(reverse_bus_search_map, PSY.get_number(PSY.get_bus(sa)))
            push!(collection, sa)
        end
    end
    return collection
end

function get_fixed_admittances(sys::PSY.System, reverse_bus_search_map)
    collection = Vector{PSY.FixedAdmittance}()
    for sa in
        collect(PSY.get_components(x -> PSY.get_available(x), PSY.FixedAdmittance, sys))
        if !haskey(reverse_bus_search_map, PSY.get_number(PSY.get_bus(sa)))
            push!(collection, sa)
        end
    end
    return collection
end

function _add_branch_to_lookup!(
    branch_lookup::Dict{String, Int},
    ::Dict{String, Vector{String}},
    branch_type::Vector{DataType},
    branch::PSY.ACTransmission,
    branch_number::Int,
)
    branch_lookup[PSY.get_name(branch)] = branch_number
    push!(branch_type, typeof(branch))
    return
end

function _add_branch_to_lookup!(
    branch_lookup::Dict{String, Int},
    transformer_3w_lookup::Dict{String, Vector{String}},
    branch_type::Vector{DataType},
    branch::PSY.ThreeWindingTransformer,
    branch_number::Int,
)
    tr3w_name = PSY.get_name(branch)
    transformer_3w_lookup[tr3w_name] = Vector{String}(undef, 3)
    for (i, side) in enumerate(["primary", "secondary", "tertiary"])
        side_name = "$(tr3w_name)__$side"
        branch_lookup[side_name] = branch_number - 3 + i
        transformer_3w_lookup[tr3w_name][i] = side_name
        push!(branch_type, typeof(branch))
    end
    return
end

"""
Gets the indices  of the reference (slack) buses.
NOTE:
- the indices  corresponds to the columns of zeros belonging to the PTDF matrix.
- BA and ABA matrix miss the columns related to the reference buses.
"""
function find_slack_positions(buses)
    return find_slack_positions(buses, make_ax_ref(buses))
end

function find_slack_positions(buses, bus_lookup::Dict{Int, Int})::Set{Int}
    slack_position = sort([
        bus_lookup[PSY.get_number(n)]
        for n in buses if PSY.get_bustype(n) == ACBusTypes.REF
    ])
    if length(slack_position) == 0
        error("Slack bus not identified in the Bus/buses list, can't build NetworkMatrix")
    end
    return Set{Int}(slack_position)
end

"""
Validates that the user bus input is consistent with the ybus axes and the prior reductions.
Is used to check `irreducible_buses` for `Radial` and `DegreeTwo` reductions and `study_buses` for `WardReduction`.
"""
function validate_buses(A::PowerNetworkMatrix, buses::Set{Int})
    reverse_bus_search_map = get_network_reduction_data(A).reverse_bus_search_map
    for bus_no in buses
        reduced_bus_no = get(reverse_bus_search_map, bus_no, bus_no)
        if reduced_bus_no ∉ get_bus_axis(A)
            if bus_no == reduced_bus_no
                error(
                    "Invalid bus entry found: Bus $bus_no. Check your input data; this bus was not found in the admittance matrix.",
                )
            else
                error(
                    "Invalid bus entry found: Bus $bus_no. Check your input data; this bus was mapped to bus $reduced_bus_no in a prior reductions and not found in the admittance matrix.",
                )
            end
        end
    end
    return
end

"""
Convert the user input for irreducible_buses to a set of indices based on the Ybus lookup and the prior reductions.
"""
function get_irreducible_indices(A::AdjacencyMatrix, irreducible_buses::Vector{Int})
    reverse_bus_search_map = A.network_reduction_data.reverse_bus_search_map
    irreducible_indices = zeros(Int, length(irreducible_buses))
    for (ix, bus_no) in enumerate(irreducible_buses)
        reduced_bus_no = get(reverse_bus_search_map, bus_no, bus_no)
        irreducible_indices[ix] = A.lookup[1][reduced_bus_no]
    end
    return irreducible_indices
end

"""
Evaluates the ABA matrix given the System's Incidence matrix (A), BA matrix and
reference bus positions.

# Arguments
- `A::SparseArrays.SparseMatrixCSC{Int8, Int}`:
        Incidence matrix.
- `BA::SparseArrays.SparseMatrixCSC{Float64, Int}`
        BA matrix.

NOTE:
- evaluates A with "calculate_A_matrix", or extract A.data (if A::IncidenceMatrix)
- evaluates BA with "calculate_BA_matrix", or extract BA.data (if A::BA_Matrix)
"""
function calculate_ABA_matrix(
    A::SparseArrays.SparseMatrixCSC{Int8, Int},
    BA::SparseArrays.SparseMatrixCSC{Float64, Int},
    ref_bus_positions::Set{Int})
    tmp = BA * A
    valid_ix = setdiff(1:size(tmp, 1), ref_bus_positions)
    return tmp[valid_ix, valid_ix]
end

"""
Return a sparse matrix given a dense one by dropping elements whose absolute
value is below a certain tolerance.

Uses optimized `droptol!` for better performance compared to element-wise iteration.

# Arguments
- `dense_array::Matrix{Float64}`:
        input matrix (e.g., PTDF matrix).
- `tol::Float64`:
        tolerance.
"""
function sparsify(dense_array::Matrix{Float64}, tol::Float64)
    sparse_array = SparseArrays.sparse(dense_array)
    SparseArrays.droptol!(sparse_array, tol)
    return sparse_array
end

"""
Return a sparse vector given a dense one by dropping elements whose absolute
value is below a certain tolerance.

Uses optimized `droptol!` for better performance compared to element-wise iteration.

# Arguments
- `dense_array::Vector{Float64}`:
        input vector (e.g., PTDF row from VirtualPTDF).
- `tol::Float64`:
        tolerance.
"""
function sparsify(dense_array::Vector{Float64}, tol::Float64)
    sparse_array = SparseArrays.sparsevec(dense_array)
    SparseArrays.droptol!(sparse_array, tol)
    return sparse_array
end

"""
    _get_equivalent_physical_branch_parameters(equivalent_ybus::Matrix{$YBUS_ELTYPE})

Takes as input a 2x2 Matrix{$YBUS_ELTYPE} representing the Ybus contribution of either an
AbstractBranchesParallel (homogeneous or mixed) or BranchesSeries object.
Returns a dictionary of equivalent parameters, matching the PowerModels data format.
"""
function _get_equivalent_physical_branch_parameters(equivalent_ybus::Matrix{YBUS_ELTYPE})
    y_11, y_12, y_21, y_22 = equivalent_ybus
    if isapprox(y_12, y_21)
        tap = 1.0
        shift = 0.0
    else
        tap = 1.0
        ratio = log(y_21 / y_12) / 2
        if !isapprox(0.0, real(ratio); atol = 1e-6)
            error(
                "Equivalent parameters for the series or parallel reduction of branches results \
          in a real part of the phase shift angle. This indicates invalid data for the branches being reduced \
          possible due to branches in parallel with different phase angles.",
            )
        end
        shift = imag(ratio)
    end
    y_l = y_12 * -1 * exp(1 * shift * im)
    z_12 = 1 / y_l
    r = real(z_12)
    x = imag(z_12)
    g_from = real(y_11 - y_l)
    b_from = imag(y_11 - y_l)
    g_to = real(y_22 - y_l)
    b_to = imag(y_22 - y_l)
    return EquivalentBranch(r, x, g_from, b_from, g_to, b_to, tap, shift)
end

is_a_reduction(::PSY.ACTransmission) = false

function has_time_series(
    branch::BranchesSeries,
    ts_type::Type{T},
    ts_name::String,
) where {
    T <: PSY.TimeSeriesData,
}
    for b in branch
        if is_a_reduction(b)
            if has_time_series(b, ts_type, ts_name)
                return true
            end
            continue
        end

        if has_time_series(b, ts_type, ts_name)
            return true
        end
    end
    return false
end

function has_time_series(
    branch::AbstractBranchesParallel,
    ts_type::Type{T},
    ts_name::String,
) where {
    T <: PSY.TimeSeriesData,
}
    for b in branch
        if PSY.has_time_series(b, ts_type, ts_name)
            return true
        end
    end
    return false
end

function has_time_series(
    branch::PSY.ACTransmission,
    ts_type::Type{T},
    ts_name::String,
) where {
    T <: PSY.TimeSeriesData,
}
    if PSY.has_time_series(branch, ts_type, ts_name)
        return true
    end
    return false
end

function get_device_with_time_series(
    branch::BranchesSeries,
    ts_type::Type{T},
    ts_name::String,
) where {
    T <: PSY.TimeSeriesData,
}
    for b in branch
        if has_time_series(b, ts_type, ts_name)
            return get_device_with_time_series(b, ts_type, ts_name)
        end
    end
    return nothing
end

function get_device_with_time_series(
    branch::AbstractBranchesParallel,
    ts_type::Type{T},
    ts_name::String,
) where {
    T <: PSY.TimeSeriesData,
}
    for b in branch
        if has_time_series(b, ts_type, ts_name)
            return b
        end
    end
    return nothing
end

function get_device_with_time_series(
    branch::PSY.ACTransmission,
    ts_type::Type{T},
    ts_name::String,
) where {
    T <: PSY.TimeSeriesData,
}
    if has_time_series(branch, ts_type, ts_name)
        return branch
    end
    return nothing
end

"""
    _resolve_branch_arc(nr::NetworkReductionData, component::PSY.ACTransmission)
        -> Tuple{Symbol, Union{Tuple{Int, Int}, Nothing}}

Classify a branch component by looking up which reverse map it belongs to in the
`NetworkReductionData`. Returns `(tag, arc_tuple)` where `tag` is one of:
- `:direct`       -- branch is the sole branch on its arc
- `:parallel`     -- branch is one of several parallel branches on its arc
- `:series`       -- branch is part of a series chain on its arc
- `:transformer3w` -- branch is a three-winding transformer winding
- `:not_found`    -- branch is not in any map (e.g., eliminated by radial reduction)

The second element is the arc tuple `(from_bus, to_bus)`, or `nothing` when `:not_found`.
"""
function _resolve_branch_arc(
    nr::NetworkReductionData,
    component::PSY.ACTransmission,
)::Tuple{Symbol, Union{Tuple{Int, Int}, Nothing}}
    if haskey(nr.reverse_direct_branch_map, component)
        return (:direct, nr.reverse_direct_branch_map[component])
    elseif haskey(nr.reverse_parallel_branch_map, component)
        return (:parallel, nr.reverse_parallel_branch_map[component])
    elseif haskey(nr.reverse_series_branch_map, component)
        return (:series, nr.reverse_series_branch_map[component])
    elseif haskey(nr.reverse_transformer3W_map, component)
        return (:transformer3w, nr.reverse_transformer3W_map[component])
    else
        return (:not_found, nothing)
    end
end

"""
    _assert_not_phase_shifting(component::PSY.ACTransmission)

No-op for non-PST branches. Throws `ErrorException` for `PhaseShiftingTransformer`.
"""
_assert_not_phase_shifting(::PSY.ACTransmission) = nothing

function _assert_not_phase_shifting(component::PSY.PhaseShiftingTransformer)
    return error(
        "Contingencies on PhaseShiftingTransformer are not supported. " *
        "Component: $(PSY.get_name(component)).",
    )
end

"""
    _segment_susceptance_after_outage(segment, tripped_set) -> Float64

Compute the remaining susceptance of a series chain segment after removing
tripped components. Dispatches on segment type to handle both single branches
and parallel groups within a series chain.

Returns 0.0 if the segment (or all branches in a parallel group) is fully tripped.
"""
function _segment_susceptance_after_outage(
    segment::PSY.ACTransmission,
    tripped_set::Set{<:PSY.ACTransmission},
)::Float64
    return segment ∈ tripped_set ? 0.0 : get_series_susceptance(segment, PSY.SU)
end

function _segment_susceptance_after_outage(
    segment::AbstractBranchesParallel,
    tripped_set::Set{<:PSY.ACTransmission},
)::Float64
    b_remaining = 0.0
    for branch in segment.branches
        if branch ∉ tripped_set
            b_remaining += get_series_susceptance(branch, PSY.SU)
        end
    end
    return b_remaining
end

"""
    _compute_series_outage_delta_b(series_chain::BranchesSeries, component::PSY.ACTransmission) -> Float64

Compute the change in equivalent arc susceptance when `component` is tripped
from `series_chain`. Delegates to the vector version.
"""
function _compute_series_outage_delta_b(
    series_chain::BranchesSeries,
    component::PSY.ACTransmission,
)::Float64
    return _compute_series_outage_delta_b(series_chain, [component])
end

"""
    _compute_series_outage_delta_b(series_chain::BranchesSeries, tripped::Vector{<:PSY.ACTransmission}) -> Float64

Compute the change in equivalent arc susceptance when multiple components are
simultaneously tripped from a series chain.

For a series chain with segments of susceptance b₁, b₂, ..., bₙ, the equivalent
susceptance is: b_eq = 1 / (1/b₁ + 1/b₂ + ... + 1/bₙ).

Segments can be individual branches or `BranchesParallel` groups. When a tripped
component is inside a parallel group, only that branch's susceptance is removed
from the group — the rest of the parallel group remains in the series chain.

Returns Δb = b_new - b_old (always negative for outages).
If all segments are fully tripped, returns -b_eq (full arc outage).
"""
function _compute_series_outage_delta_b(
    series_chain::BranchesSeries,
    tripped::Vector{<:PSY.ACTransmission},
)::Float64
    b_old = get_series_susceptance(series_chain, PSY.SU)
    tripped_set = Set{PSY.ACTransmission}(tripped)
    remaining_inv_sum = 0.0
    for segment in series_chain
        b_seg = _segment_susceptance_after_outage(segment, tripped_set)
        if b_seg == 0.0
            return -b_old
        end
        remaining_inv_sum += 1.0 / b_seg
    end
    b_new = 1.0 / remaining_inv_sum
    return b_new - b_old
end
